plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'online.hatsune_miku'
version = '0.0.1-SNAPSHOT'
description = 'BookWiki'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'

    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation("org.springframework.boot:spring-boot-starter-webmvc-test")
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.apache.tika:tika-parsers-standard-package:3.2.3'
    implementation 'org.apache.tika:tika-core:3.2.3'
    implementation 'org.apache.poi:poi-ooxml:5.4.0'
    implementation 'com.github.librepdf:openpdf:2.0.3'
    implementation 'org.jsoup:jsoup:1.18.3'
    //spring-boot-webmvc-test
}

tasks.named('test') {
    useJUnitPlatform()
}

// Frontend Build Integration

def frontendDir = "$projectDir/frontend"
def staticResourceDir = "$projectDir/src/main/resources/static"

task installFrontend(type: Exec) {
    workingDir frontendDir
    commandLine 'bun', 'install'
    // Only run if package.json has changed (optimization)
    inputs.file "$frontendDir/package.json"
    outputs.dir "$frontendDir/node_modules"
}

task buildFrontend(type: Exec) {
    dependsOn installFrontend
    workingDir frontendDir
    commandLine 'bun', 'run', 'build'
    inputs.dir "$frontendDir/src"
    outputs.dir "$frontendDir/dist"
}

task copyFrontend(type: Sync) {
    dependsOn buildFrontend
    from "$frontendDir/dist"
    into staticResourceDir
}

// Hook into the standard build process
processResources {
    dependsOn copyFrontend
}

clean {
    delete staticResourceDir
    delete "$projectDir/releases"
}

// Standalone Installer Automation

task bundleJre(type: Exec) {
    workingDir "$projectDir/scripts"
    commandLine 'cmd', '/c', 'bundle-jre.bat'
}

task buildSidecar(type: Exec) {
    workingDir "$projectDir/scripts/wrapper"
    commandLine 'cargo', 'build', '--release'
    inputs.dir "$projectDir/scripts/wrapper/src"
    inputs.file "$projectDir/scripts/wrapper/Cargo.toml"
    outputs.dir "$projectDir/scripts/wrapper/target/release"
}

task prepareDist(type: Copy) {
    dependsOn build, bundleJre, buildSidecar
    from "$projectDir/build/libs/BookWiki-0.0.1-SNAPSHOT.jar"
    from "$projectDir/scripts/wrapper/target/release/bookwiki-backend.exe"
    into "$projectDir/scripts/dist"

    rename 'bookwiki-backend.exe', 'bookwiki-backend-x86_64-pc-windows-gnu.exe'
}

task buildTauri(type: Exec) {
    dependsOn prepareDist
    workingDir frontendDir
    // Copy the sidecar to src-tauri as well (required for bundling)
    doFirst {
        copy {
            from "$projectDir/scripts/dist/bookwiki-backend-x86_64-pc-windows-gnu.exe"
            into "$frontendDir/src-tauri"
        }
    }
    commandLine 'bun', 'tauri', 'build', '--target', 'x86_64-pc-windows-gnu'
}

task packageInstaller(type: Copy) {
    description = 'Builds the Tauri app and moves installers to the releases directory'
    group = 'distribution'
    dependsOn buildTauri

    // Ensure releases directory exists
    doFirst {
        mkdir "$projectDir/releases"
    }

    from "$frontendDir/src-tauri/target/x86_64-pc-windows-gnu/release/bundle/msi"
    from "$frontendDir/src-tauri/target/x86_64-pc-windows-gnu/release/bundle/nsis"
    into "$projectDir/releases"

    include "*.msi", "*.exe"
}
